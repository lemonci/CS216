#/*# Start of file funct7.a */
#
#/*# MIPSMARK 1.0 1/5/98 Copyright 1998 J. Waldron. */
#
## Question:
##
## Write a function "strchk" that returns
## the number of characters in a string $a0
## that return a 1 when passed to the
## "checkch" function.
## Otherwise checkch returns 0.
## do not rely on the label "str" or the 
## particular implementation of "checkch"
## 

#/*# Output format must be:		*/
#/*# "number of characters that pass test = 3"		*/
#include <stdio.h>       /* for printf in C programs */
#include <stdlib.h>	 /* for exit() in C programs */


#/*##############################################
#					 	#
#		text segment			#
#						#
#################################################

	.text		
       	.globl __start 
__start:		# execution starts here  */


	la $a0,ans
	li $v0,4	# print out string
	syscall 
	
	la $a0,str
	jal strchk	# call strchk function

	move $a0,$v0
	li $v0,1	# print out returned value
	syscall 

	la $a0,endl	# system call to print
	li $v0,4	# out a newline
	syscall

	li $v0,10
	syscall		# au revoir...


#
# checkch is a function which takes a character
# in a0,  and returns either 0 or 1 in v0,
# depending on the result of some test.
# Do not rely of the particular operation performed
# by checkch or the registers used.
# These will be different in other mipsmark cases.
#

checkch:
        li $v0,0
        beq  $a0,'a',yes
        beq  $a0,'e',yes
        beq  $a0,'i',yes
        beq  $a0,'o',yes
        beq  $a0,'u',yes
        jr $ra
yes:    li $v0,1
        jr $ra

#/* Any changes above this line will be discarded by
# mipsmark. Put your answer between dashed lines. */
#/*-------------- start cut ----------------------- */

#/*  Student's Name: Mengqi	Li	Account: mli		*/

#/*
#	j __start	#nasty loop if mips program not exited */


strchk:
		sub $sp, $sp, 16  # save registers on stack
		sw $a0, 0 ($sp)
		sw $s0, 4 ($sp)
		sw $s1, 8 ($sp)
		sw $ra, 12 ($sp)

        li $s0, 0         # s0 will be the count of vowels
		move $s1, $a0     # address of string
	
nextc:	lb $a0, ($s1)     # get each character
		beqz $a0, done    # zero marks end
		jal checkch       # call checkch
		add $s0, $s0, $v0 # add 0 or 1 to count
		add $s1, $s1, 1   # move along string
		b nextc
done:   move $v0, $s0     # use $v0 for result

		lw $a0, 0($sp)    # restore registers
		lw $s0, 4 ($sp)
		lw $s1, 8 ($sp)
		lw $ra, 12 ($sp)
		add $sp, $sp, 16
        jr $ra

#/*--------------  end cut  -----------------------
# Any changes below this line will be discarded by
# mipsmark. Put your answer between dashed lines.

#################################################
#                                               #
#               data segment                    #
#                                               #
#################################################

        .data			#*/
str:	.asciiz "aei0956xyz\n"
ans:	.asciiz "number of characters that pass test = "
endl:	.asciiz "\n"
#
#/*# End of file funct7.a		*/
